float g_announcer_print;

void(entity player, string msg, string txt) announce_force {
	entity e;
	if (player.classname == "player")
		CLIENT_FOR_EACH_REAL(e) {
			if (SPECTATOR_IS_SPECTATING(e, player)) {
				announce_force(e, msg, txt);
			}
		}
	if (clienttype(player) != CLIENTTYPE_REAL)
		return;

	if (SPECTATOR_IS_SPECTATOR(player))
		return;

	player.announcetime = time + 0.8;
	if (msg != "")
		sound_play2(player, msg);

	if (txt != "")
	if (g_announcer_print) {
		RM_ELSE({
			rm_bigprint(player, txt, "", 2);
		},{
			msg_entity = player;
			net_write_byte(MSG_ONE, SVC_TEMPENTITY);
			net_write_byte(MSG_ONE, NET_TE_CSQC_ANNOUNCE_PRINT);
			net_write_string(MSG_ONE, txt);
		})
	}
}

void(entity player, string msg, string txt) announce {
	if (time > player.announcetime)
		announce_force(player, msg, txt);
}

void(void) announce_init {
	CVAR_CACHE(g_announcer_print);
}
